group 'com.instamotor'
version '1.5.2'

buildscript {
    ext.kotlin_version = '1.5.31'

    repositories {
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "com.github.gmazzo:gradle-buildconfig-plugin:3.0.0"
    }
}


allprojects {
    apply plugin: 'kotlin'
    apply plugin: 'idea'
    apply plugin: "com.github.gmazzo.buildconfig"
    apply plugin: 'maven-publish'
    apply plugin: 'signing'

    buildConfig {
        buildConfigField "String", "VERSION", "\"${rootProject.version}\""
        buildConfigField 'List<String>', 'TESTED_GRADLE_VERSIONS', 'listOf("6.5", "6.6", "6.7.1", "7.0", "7.2")'
    }

    repositories {
        mavenCentral()
        jcenter()
    }

    sourceSets {
        main {
            java {
                srcDirs += "${buildDir.absolutePath}/gen/buildconfig/src/main"
            }
        }
    }
}

def gitTag() {
    def tag = 'git tag --list --points-at HEAD'.execute((List) null, rootProject.projectDir).text.trim()

    if (tag.split(System.lineSeparator()).length > 1) {
        throw new IllegalStateException("gitTag is accessed but commit has multiple tags: $tag")
    }

    return tag
}

def validateTagAndVersion() {
    if (gitTag().isEmpty()) {
        throw new IllegalStateException('Publishing is not allowed because current commit has no tag')
    }

    if (version != gitTag().substring(1)) {
        throw new IllegalStateException('Publishing is not allowed git tag version != project version')
    }
}
